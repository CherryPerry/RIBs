configureAndroidApp(project)

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.badoo.ribs.sandbox"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    if (useCompose.toBoolean()) {
        buildFeatures {
            compose true
        }

        composeOptions {
            kotlinCompilerVersion deps.versions.kotlin
            kotlinCompilerExtensionVersion deps.versions.compose
        }
    }
}

androidExtensions {
    experimental = true
    features = ["parcelize"]
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=com.badoo.ribs.annotation.ExperimentalApi"
        if (useCompose.toBoolean()) {
            useIR = true
        }
    }
}

dependencies {
    compileOnly deps.apt.javax

    implementation deps.android.appCompat
    implementation deps.android.constraintLayout
    implementation deps.android.design
    implementation deps.android.percent


    implementation deps.external.dagger
    implementation deps.external.mviCore
    implementation deps.external.mviCoreAndroid
    implementation deps.external.rxrelay2
    implementation deps.external.rxandroid2
    implementation deps.external.leakCanary // use debugImplementation in real life apps

    implementation project(":libraries:rib-base")
    implementation project(":libraries:rib-debug-utils")
    implementation project(":libraries:rib-mvicore")
    implementation project(":libraries:rib-portal-rx")
    implementation project(":libraries:rib-recyclerview")
    implementation project(':libraries:rib-rx2')

    if (useCompose.toBoolean()) {
        implementation project(":libraries:rib-compose")
        implementation project(":sandbox-compose")
        implementation deps.compose.runtime
        implementation deps.compose.ui
        implementation deps.compose.material
        implementation deps.compose.uiTooling
        implementation deps.compose.foundation
    } else {
        implementation project(":sandbox-compose-placeholder")
    }

    testImplementation deps.external.roboelectricBase
    testImplementation deps.test.junit
    testImplementation deps.test.mockitoKotlin
    testImplementation deps.test.assertj
    testImplementation project(":libraries:rib-base-test")
    testImplementation project(":libraries:rib-base-test-rx2")

    debugImplementation project(":libraries:rib-base-test-activity")
    androidTestImplementation project(":libraries:rib-base-test")
    androidTestImplementation deps.androidTest.espresso
    androidTestImplementation deps.androidTest.runner
    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
}

repositories {
    mavenCentral()
}
