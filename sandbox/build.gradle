configureAndroidApp(project)

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.badoo.ribs.sandbox"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    if (useCompose) {
        buildFeatures {
            compose true
        }

        composeOptions {
            kotlinCompilerVersion "1.3.70-dev-withExperimentalGoogleExtensions-20200424"
            kotlinCompilerExtensionVersion "0.1.0-dev14"
        }
    }
}

androidExtensions {
    experimental = true
    features = ["parcelize"]
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=com.badoo.ribs.annotation.ExperimentalApi"
    }
}

dependencies {
    implementation deps.external.kotlinStdlib
    compileOnly deps.apt.javax

    implementation deps.android.appCompat
    implementation deps.android.constraintLayout
    implementation deps.android.design
    implementation deps.android.percent


    implementation deps.external.dagger
    implementation deps.external.mviCore
    implementation deps.external.mviCoreAndroid
    implementation deps.external.rxrelay2
    implementation deps.external.rxandroid2

    implementation project(":libraries:rib-base")
    implementation project(":libraries:rib-portal-rx")
    implementation project(":libraries:rib-recyclerview")
    implementation project(":libraries:rib-workflows-rx")

    if (useCompose) {
        implementation project(":libraries:rib-compose")
        implementation project(":sandbox-compose")
        implementation deps.compose.runtime
        implementation deps.compose.uiCore
        implementation deps.compose.uiLayout
        implementation deps.compose.uiMaterial
        implementation deps.compose.uiTooling
        implementation deps.compose.uiFoundation
    } else {
        implementation project(":sandbox-compose-placeholder")
    }

    testImplementation deps.external.roboelectricBase
    testImplementation deps.test.junit
    testImplementation deps.test.mockitoKotlin
    testImplementation deps.test.assertj
    testImplementation project(":libraries:rib-base-test")

    debugImplementation project(":libraries:rib-base-test-activity")
    androidTestImplementation project(":libraries:rib-base-test")
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
}

repositories {
    mavenCentral()
}
